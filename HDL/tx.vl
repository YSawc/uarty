module Tx #(
    parameter CLOCK_FREQUENCY: logic<30> = 32'd50000000,
    parameter BAUD_RATE      : logic<30> = 32'd115200  ,

) (
    CLK     : input  logic   ,
    RST     : input  logic   ,
    WE      : input  logic   ,
    DATA_IN : input  logic<8>,
    data_out: output logic   ,
    busy    : output logic   ,
    finish  : output logic   ,
) {
    var wait_div    : logic<32>;
    assign wait_div     = CLOCK_FREQUENCY / BAUD_RATE;
    var wait_count  : logic<10>;
    var n_wait_count: logic<10>;
    var n_busy      : logic    ;
    enum Status: logic<3>  {
        idle,
        send,
    }
    var status     : Status    ;
    var n_status   : Status    ;
    var data_reg   : logic <10>;
    var n_data_reg : logic <10>;
    var bit_count  : logic <5> ;
    var n_bit_count: logic <5> ;
    var n_finish   : logic     ;

    assign data_out = data_reg[0];

    always_comb {
        n_wait_count = wait_count;
        n_bit_count  = bit_count;
        n_status     = status;
        n_data_reg   = data_reg;
        n_busy       = busy;
        n_finish     = finish;
        case status {
            Status::idle: {
                n_busy   = 0;
                n_finish = 0;
                if WE {
                    if wait_count == wait_div {
                        n_wait_count = 0;
                        if bit_count == 9 {
                            n_status    = Status::send;
                            n_data_reg  = {1'b1, DATA_IN, 1'b0};
                            n_bit_count = 0;
                        } else {
                            n_data_reg  =  {10'h3ff};
                            n_bit_count += 1;
                        }
                    } else {
                        n_wait_count += 1;
                    }
                }
            }
            Status::send: {
                n_busy = 1;
                if wait_count == wait_div {
                    n_wait_count = 0;
                    if bit_count == 9 {
                        n_status    = Status::idle;
                        n_bit_count = 0;
                        n_finish    = 1;
                    } else {
                        n_status    =  Status::send;
                        n_data_reg  =  {1'b1, data_reg[9:1]};
                        n_bit_count += 1;
                    }
                } else {
                    n_wait_count += 1;
                }
            }
        }
    }

    always_ff (posedge CLK, async_high RST) {
        if_reset {
            status     = Status::idle;
            wait_count = 0;
            data_reg   = 10'h3ff;
            bit_count  = 0;
            busy       = 0;
            finish     = 0;
        } else {
            status     = n_status;
            wait_count = n_wait_count;
            data_reg   = n_data_reg;
            bit_count  = n_bit_count;
            busy       = n_busy;
            finish     = n_finish;
        }
    }
}
